plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

// gradle attributes
group = project.property('groupId')
version = '1.0-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

dependencies {

    // Use JUnit test framework
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.1'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.5.1'

    // XSD Parser
    implementation group: 'com.github.xmlet', name: 'xsdParser', version: '1.0.23'
    implementation group: 'in.jlibs', name: 'jlibs-xsd', version: '2.2.3'
    implementation group: 'org.glassfish.jaxb', name: 'xsom', version: '2.3.3-b02'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.0'

    // This dependency is used for json to csv parsing
    implementation group: 'com.github.ramalapure', name: 'json-parser', version: '1.1'

}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = project.property('artifactId')
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                packaging = 'jar'
                name = project.property('name')
                description = project.property('description')
                url = project.property('gitUrl')
                licenses {
                    license {
                        name = project.property('licenseName')
                        url = project.property('licenseUrl')
                    }
                }
                developers {
                    developer {
                        id = project.property('developerId')
                        name = project.property('developerName')
                        email = project.property('developerEmail')
                    }
                }
                scm {
                    connection = project.property('scmConnection')
                    developerConnection = project.property('scmDeveloperConnection')
                    url = project.property('gitUrl')
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? project.property('snapshotsRepoUrl') : project.property('releasesRepoUrl')
            credentials {
                username = project.property('nexusUsername')
                password = project.property('nexusPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}